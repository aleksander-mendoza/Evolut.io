#version 450
//#extension GL_EXT_debug_printf : enable
#define IS_AVAILABLE_BUFFER_GLOBAL_MUTABLES
#define IS_AVAILABLE_BUFFER_WORLD
#define IS_AVAILABLE_BUFFER_PLAYER_EVENT
#define IS_AVAILABLE_BUFFER_INDIRECT
#define IS_AVAILABLE_BUFFER_FACES

layout (local_size_x = 1) in;

#include "descriptors_compute.comp"
#include "utils.comp"

void main()
{

    uint gID = gl_GlobalInvocationID.x;

    if (player_event.event_type == PLAYER_EVENT_SET_BLOCK){
        // vec3_slot0 holds current player position
        // vec3_slot1 holds ray cast direction
        // u32_slot0 holds ray cast direction
        const RayCastResult ray = ray_cast(player_event.vec3_slot0, player_event.vec3_slot1);
        if (ray.found){
            const uvec3 hit_pos = uvec3(player_event.u32_slot0>0?ray.prev_v:ray.v);
            const uint hit_idx = block_pos_into_world_idx(hit_pos);
            uint prev_block = world[hit_idx];
            world[hit_idx] = player_event.u32_slot0;
            if (player_event.u32_slot0==0){
                const uint number_of_faces = remove_block_faces(BLOCK_PROPS[prev_block], hit_pos);
                draw_indirect[DRAW_INDIRECT_BLOCK].instance_count = number_of_faces;
            } else {
                const uint number_of_faces = add_block_faces(hit_pos, player_event.u32_slot0);
                draw_indirect[DRAW_INDIRECT_BLOCK].instance_count = number_of_faces;
            }
        }
    }
    const int ambience_tick = global_mutables.ambience_tick;
    global_mutables.ambience_tick = ambience_tick < AMBIENCE_MAX_TIME_TICK-1 ? ambience_tick+1 : 0;
    global_mutables.blocks_to_be_inserted_or_removed = 0;
    const uint world_blocks_to_update_even = global_mutables.world_blocks_to_update_even;
    const uint world_blocks_to_update_odd = 1 - world_blocks_to_update_even;
    global_mutables.world_blocks_to_update[world_blocks_to_update_even] = 0;
    const uint world_blocks_to_update_count = global_mutables.world_blocks_to_update[world_blocks_to_update_odd];
    global_mutables.world_blocks_to_update_even = world_blocks_to_update_odd;
    set_indirect_dispatch(DISPATCH_INDIRECT_UPDATE_AMBIENCE, world_blocks_to_update_count);
}

